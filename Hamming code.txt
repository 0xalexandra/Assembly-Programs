# ----- Program: Error Detection with Hamming Code (Even Parity) -----
.text

# ----- Read CodeWord [data+parity bits (12 digits 0 or 1)] from input -----
start:
	la $t0,CodeWord 
	
	li $t1,0

firstAgain:
	bge $t1,12,control
	
	li $v0,12
	syscall
	
	addi $v0,-48
	
	sb $v0,($t0)
	
	addi $t0,$t0,1
	
	addi $t1,$t1,1
	
	j firstAgain
				
# ----- Control 4 parity digits (potitions 1, 2, 4, 8) and their data digits for even parity -----
control:
	li $t2,1
	la $t0,CodeWord

secondAgain:
	bgt $t2,4,ok
	
	li $t3,0
	
	beq $t2,1,first
	beq $t2,2,second
	beq $t2,3,third
	beq $t2,4,fourth

first:
	la $t1,PotitionsForDigit1
	j sum_calculation
	
second:
	la $t1,PotitionsForDigit2
	j sum_calculation

third:
	la $t1,PotitionsForDigit4
	j sum_calculation

fourth:
	la $t1,PotitionsForDigit8
	j sum_calculation

sum_calculation:
	lb $t4,($t1)
	beq $t4,-1,exit_loop
	add $t4,$t4,$t0
	addi $t4,$t4,-1
	lb $t5,($t4)
	add $t3,$t3,$t5
	addi $t1,$t1,1
	j sum_calculation
	
exit_loop:
	li $t7,2
	rem $t6,$t3,$t7
	bnez $t6,not_ok
	addi $t2,$t2,1
	j secondAgain
	
# ----- Print Results and exit: ----- 
ok:
	la $a0,ok_message
	li $v0,4
	syscall
	j exit

not_ok:
	la $a0,not_ok_message
	li $v0,4
	syscall
	j exit

exit:
	li$v0,10
	syscall

# ----- Data Segment: -----  

.data
CodeWord:				.byte 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0
PotitionsForDigit1: 	.byte 1, 3, 5, 7, 9, 11, -1
PotitionsForDigit2: 	.byte 2, 3, 6, 7, 10, 11, -1
PotitionsForDigit4: 	.byte 4, 5, 6, 7, 12, -1
PotitionsForDigit8: 	.byte 8, 9, 10, 11, 12, -1
not_ok_message:			.asciiz "\n - Error in CodeWord"
ok_message:				.asciiz "\n - No error in CodeWord"